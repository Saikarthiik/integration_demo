to launchURL 
	
	try
		set browser to "Chrome"
		set url to universal data.SC2ApplicationURL
		--set the AutoOCRUpdateDiagnostics to [CaseSensitive, IgnoreSpaces, TextDifference, ValidCharacters, ValidWords, DPI]
		set the ImageUpdate to Auto
		set universal pageWait to 60   #softWait
		log "Opening " & url & " using " & browser
		click [20,200]
		set universal browser to browser
		typetext windowskey,"r"
		wait 0.6
		waitfor universal pageWait, text:"Type the",searchRectangle:(screenPartition().SUT.("BLQ"))
		typetext browser
		typetext returnkey
		wait 2
		"SC2_CommonActions/sc2CommonHandlers".ClearCache
		typetext windowskey,"r"
		wait 0.6
		waitfor universal pageWait, text:"Type the",searchRectangle:(screenPartition().SUT.("BLQ"))
		typetext browser&&url
		wait 0.5
		typetext returnkey
		waitfor universal pageWait,image:"icons/refresh",searchRectangle:(screenPartition().SUT.("browserRefresh"))
		click [20,200]
		typetext windowskey, upArrow
		smartUtil.pageLoad
		
		smartUtil.completeScreenLoad
		typetext controlKey,l,Return
		smartUtil.pageLoad
		--typetext windowskey, downArrow
		--smartUtil.pageLoad
		log "ExecutionBrowser:"&"Chrome"
	Catch theException
		smartUtil.errorCapture theException
	End try
end launchURL

to logInToSC2App 
	params UserNameParam, PasswordParam
	smartUtil.validate "KEYSIGHT"
	smartUtil.clickText "KEYSIGHT Login"
	typetext windowskey, downArrow
	smartUtil.validate "KEYSIGHT"
	--smartUtil.validate"Sign in"
	If ImageFound(text:"Can't access your account?",WaitFor:90,searchRectangle:(screenPartition().SUT.("LH"))) then
		smartUtil.validate "KEYSIGHT"
		--smartUtil.validate "Next"
		smartUtil.completeScreenLoad
		smartUtil.clickText "Sign in", "topLeftQuadrant"
		smartUtil.clickTextAtSpot "Sign in", 20,50,"topLeftQuadrant"
		TypeText controlkey,a 
		typetext UserNameParam ,return  
		//set passWord to PasswordParam
		smartUtil.clickTextAtSpot "Enter password", 20,50,"fullScreen"
		TypeText controlkey,a 
		TypeText backspace
		smartUtil.validate "Enter password"
		wait 2		
		TypeEncodedText PasswordParam,UserNameParam
		typetext return
		typetext windowskey, upArrow
		--smartUtil.clickText "Never","topRightQuadrant"
		smartUtil.pageLoad
	else 
		set userName to UserNameParam
		set passWord to PasswordParam
		smartUtil.validate "Pick an account"
		smartUtil.clickText userName
		smartUtil.validate "Enter password"
		wait 2
		TypeText controlkey,a 
		TypeText backspace
		TypeEncodedText passWord,userName
		TypeText return
		#typetext passWord ,return
		Log "Logging in with the selected account: " & userName
		
	end if
	smartUtil.validate "Supply Chain Collaboration"
	wait 4
end logInToSC2App

to logoutToSC2
	Params UserName
	
	smartUtil.validate UserName,"topRightQuadrant"
	smartUtil.clickTextAtSpot UserName,"-20","1","topRightQuadrant"
	smartUtil.validate "Logout","topRightQuadrant"
	smartUtil.clickText "Logout","topRightQuadrant"
	wait 2
	if ImageFound(text:"you signed out of your account", waitFor:10)
		log "Signed out from SC2 applications"
	else if ImageFound(text:"LOGIN", waitFor:10)
		log "Signed out from SC2 applications"
	end if
	
	
end logoutToSC2

to validateLoginConsoleShouldNotHaveError
	TypeText controlKey, shiftKey,J
	
	wait 3
	smartUtil.clickImagewithHotspot "icons/eyeIconForFilterInConsole",50,1,"topRightQuadrant"
	TypeText controlKey,a
	TypeText backspace
	if ImageFound(imageName:"icons/refresh", waitFor:universal pageWait, searchRectangle:(screenPartition().SUT.("browserRefresh")))
		smartUtil.clickUsingImage "icons/refresh","browserRefresh"
	end if
	smartUtil.validate "KEYSIGHT","RH"
	smartUtil.screenShot
	
	if ImageFound(text:"TypeError", searchRectangle:(screenPartition().SUT.("RH")))
		LogError "encountered an error in the console "
		
	else
		log "There is no error present in the console tab"
	end if
	TypeText controlKey, shiftKey,i
	wait 3
end validateLoginConsoleShouldNotHaveError

to logInToSC2AsSupplier userName,passWord
	smartUtil.validate"KEYSIGHT","TH"
	if ImageFound(text:"Supplier Login",searchRectangle:(screenPartition().SUT.("HM")))
		smartUtil.validate"Supplier Login","HM"
		LogSuccess"The Application is in the Login Page Procced with the Supplier Login"
	else
		LogError "The Application not in the Login Page"
		exit All
	end if
	smartUtil.validate"Supplier Login","HM"
	smartUtil.setCustomSearchRectangle"Supplier Login",-400,20,400,600,"HM"
	smartUtil.clickText"Email","Custompart"
	smartUtil.textField userName
	smartUtil.clickText"Password","Custompart"
	smartUtil.textField passWord
	smartUtil.clickText"Login","Custompart"
	Log "Logging in with the selected account: "& userName
	smartUtil.completeScreenLoad
	smartUtil.validate "Supply Chain Collaboration Portal"
	smartUtil.clicktext "Forecast & Commit","TH"
	smartUtil.setCustomSearchRectangle "Forecast & commit", -50,10,200,300
	if ImageFound(text:"CM Commit",searchRectangle:(screenPartition().SUT.("Custompart")))
		LogSuccess "Logging in with the selected Supplier account : "& userName
	else
		LogError "The Application is not in the Supplier View"
	end if
end logInToSC2AsSupplier